#return mean temperature in Celsius per day for each weather station during 2020
PREFIX sosa: <http://www.w3.org/ns/sosa/>
PREFIX qudt: <http://qudt.org/schema/qudt/>
PREFIX wep: <http://ns.inria.fr/meteo/ontology/property/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX weo: <http://ns.inria.fr/meteo/ontology/>
prefix wevp: <http://ns.inria.fr/meteo/vocab/weatherproperty/> 
SELECT ?date ?n  (((?min_temperature+?max_temperature)/2.0) as ?avg_temperature)
{
    SELECT  ?date ?n  ((MIN(?v) - 273.15) as ?min_temperature) ((MAX(?v) - 273.15) as ?max_temperature) 
    FROM <http://ns.inria.fr/meteo/observation/2020>
    FROM <http://ns.inria.fr/meteo/weatherstation>
    WHERE 
    {
          ?obs a  weo:MeteorologicalObservation; 
          sosa:observedProperty wevp:airTemperature ;
          sosa:hasSimpleResult  ?v; 
          wep:madeByStation ?station ;
          sosa:resultTime ?t .
          ?station rdfs:label ?n ;  weo:stationID ?stationID .
       
          BIND(xsd:date(?t) as ?date) 
    } 
    GROUP BY ?n ?date
    ORDER BY ?n ?date
}


