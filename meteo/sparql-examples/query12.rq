#list weather stations and their department (insee code)
#retrieve weather stations located in alpes-maritimes department
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX wd:   <http://www.wikidata.org/entity/>
prefix weo:  <http://ns.inria.fr/meteo/ontology/>
prefix dct:  <http://purl.org/dc/terms/>
prefix wdt:  <http://www.wikidata.org/prop/direct/>
prefix wikibase: <http://wikiba.se/ontology#>
prefix bd: <http://www.bigdata.com/rdf#>
prefix geo2:    <http://www.w3.org/2003/01/geo/wgs84_pos#>

select distinct ?x ?l ?altitude ?latitude ?longitude ?deptLabel ?insee_dept_code where {
  ?x a weo:WeatherStation .
  ?x rdfs:label ?l .
  ?x weo:stationID ?id .
  ?x geo2:altitude ?altitude .
  ?x geo2:lat ?latitude .
  ?x geo2:long ?longitude .
  ?x dct:spatial ?e .
  SERVICE <https://query.wikidata.org/sparql> {    
    SELECT ?e ?dept ?deptLabel ?insee_dept_code
    WHERE
  {
    ?e wdt:P131* ?dept .
    ?dept wdt:P31 wd:Q6465 .
    ?dept rdfs:label ?deptLabel .
    ?dept wdt:P2586 ?insee_dept_code .
    FILTER (lang(?deptLabel) = "fr")
    SERVICE wikibase:label { bd:serviceParam wikibase:language "[AUTO_LANGUAGE],fr" . }  
 
}
}
}
